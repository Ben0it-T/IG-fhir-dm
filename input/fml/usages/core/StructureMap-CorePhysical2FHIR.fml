map "https://aphp.fr/ig/fhir/dm/StructureMap/CorePhysical2FHIR" = "CorePhysical2FHIR"

/// name = 'CorePhysical2FHIR'
/// title = 'Alignement de l\'usage Core du modèle physique du DPI vers les ressources FHIR'

// Alignement entre le modèle physique du DPI et les ressources FHIR de l'usage Core


uses "https://aphp.fr/ig/fhir/dm/StructureDefinition/CoreDataSet" alias DataSet as source
uses "https://aphp.fr/ig/fhir/dm/StructureDefinition/CorePatient" alias PatientTable as source
uses "https://aphp.fr/ig/fhir/dm/StructureDefinition/CoreSejour" alias SejourTable as source
uses "https://aphp.fr/ig/fhir/dm/StructureDefinition/CoreBiology" alias BiologyTable as source
uses "https://aphp.fr/ig/fhir/dm/StructureDefinition/CoreClinical" alias ClinicalTable as source
uses "https://aphp.fr/ig/fhir/dm/StructureDefinition/CoreMedicationAdm" alias MedicationAdmTable as source
uses "https://aphp.fr/ig/fhir/dm/StructureDefinition/CoreMedicationPre" alias MedicationPreTable as source
uses "http://hl7.org/fhir/StructureDefinition/Bundle" alias FHIRBundle as target
uses "http://hl7.org/fhir/StructureDefinition/Patient" alias FHIRPatient as target
uses "http://hl7.org/fhir/StructureDefinition/Encounter" alias FHIREncounter as target
uses "http://hl7.org/fhir/StructureDefinition/Observation" alias FHIRObservation as target
uses "http://hl7.org/fhir/StructureDefinition/Meta" alias FHIRMeta as target

group dataSet(source srcDataSet : DataSet, target tgtBundle : FHIRBundle) <<types>> {
  srcDataSet -> tgtBundle.id = uuid() "setId";
  srcDataSet -> tgtBundle.type = 'transaction' "setType";
  srcDataSet.patient as srcPatient -> tgtBundle.entry as tgtPatientEntry, tgtPatientEntry.resource = create("Patient") as newPatient then {
    srcPatient then patient(srcPatient, newPatient) "setPatient";
    srcPatient then setEntryRequestAndFullUrl(newPatient,tgtPatientEntry) "setRequestAndFullUrl";
    srcDataSet.sejour as srcSejour -> tgtBundle.entry as tgtEncounterEntry, tgtEncounterEntry.resource = create("Encounter") as newEncounter then {
      srcSejour then encounter(srcSejour, newPatient, newEncounter) "setEncounter";
      srcSejour then setEntryRequestAndFullUrl(newEncounter,tgtEncounterEntry) "setRequestAndFullUrl";
      srcDataSet.biology as srcBiology where sejourNi.reference = ('SejourCore/' + %srcSejour.sejourNi.toString()) -> tgtBundle.entry as tgtBiologyEntry, tgtBiologyEntry.resource = create("Observation") as newObservation then {
        srcBiology then observationBiol(srcBiology, newPatient, newEncounter, newObservation) "setBiology";
        srcBiology then setEntryRequestAndFullUrl(newObservation,tgtBiologyEntry) "setRequestAndFullUrl";
      } "createObservation";
    } "createEncounter";
  } "createPatient";
}

group patient(source srcPatient : PatientTable, target tgtPatient : FHIRPatient) {
  srcPatient -> tgtPatient.id = uuid() "setId";
  srcPatient.patientNi as srcNi -> tgtPatient.meta as tgtMeta then setPatientMeta(srcNi, tgtMeta) "setMeta"; // copie la valeur du champ patientNi de la table Patient dans l'élément id de la ressource FHIR Patient
  srcPatient -> tgtPatient.name as tgtPatientName then setHumainName(srcPatient, tgtPatientName) "setHumainName";
  srcPatient.gender as srcPatientGender -> tgtPatient.gender = translate(srcPatientGender, 'https://aphp.fr/ig/fhir/dm/ConceptMap/DpiGender2Hl7Gender', 'code') "setGender"; // traduit la valeur du champ gender de la table Patient dans l'élément gender de la ressource FHIR Patient
  srcPatient.birthDate as srcPatientBirthDate -> tgtPatient.birthDate = srcPatientBirthDate "setBirthDate"; // copie la valeur du champ birthDate de la table Patient dans l'élément birthDate de la ressource FHIR Patient
}

group setPatientMeta(source srcNi, target tgtMeta : FHIRMeta) {
  srcNi -> tgtMeta.source = ('https://aphp.fr/fhir/Endpoint/dpi/PatientTable' + '/' + srcNi.toString()) "setSource";
}

group setHumainName(source srcPatient : PatientTable, target tgtPatientName) {
  srcPatient -> tgtPatientName.use = c('http://hl7.org/fhir/name-use', 'usual') "setUse";
  srcPatient -> tgtPatientName.text = (%srcPatient.firstName + ' ' + %srcPatient.name) "setText";
  srcPatient.name as srcPatientName -> tgtPatientName.family = srcPatientName "setName";
  srcPatient.firstName as srcPatientFirstName -> tgtPatientName.given = srcPatientFirstName "setFirstName";
}

group encounter(source srcSejour : SejourTable, source srcPatient : FHIRPatient, target tgtEncounter : FHIREncounter) {
  srcSejour -> tgtEncounter.id = uuid() "setId";
  srcSejour.sejourNi as srcNi -> tgtEncounter.meta as tgtMeta then setEncounterMeta(srcNi, tgtMeta) "setMeta"; // copie la valeur du champ patientNi de la table Patient dans l'élément id de la ressource FHIR Patient
  srcSejour -> tgtEncounter.status = c('http://hl7.org/fhir/encounter-status', 'completed') "setStatus";
  srcSejour.sejourType as srcSejourType -> tgtEncounter.type = translate(srcSejourType, 'https://aphp.fr/ig/fhir/dm/ConceptMap/DpiEncounterType2SemanticLayerEncounterType', 'CodeableConcept') "setEncounterType";
  srcPatient.id as patientId -> tgtEncounter.subject = create("Reference") as newSubject then setSubjectReference(patientId, newSubject) "setSubjectReference"; 
  srcSejour.patientNi "patientNi";// fk vers le patient
  srcSejour -> tgtEncounter.period = create("Period") as newActualPeriod then {
    srcSejour.sejourStart as srcPeriodStart -> newActualPeriod.start = srcPeriodStart "setStart";
    srcSejour.sejourEnd as srcPeriodEnd -> newActualPeriod.end = srcPeriodEnd "setEnd";
  } "setPeriod";
}

group setEncounterMeta(source srcNi, target tgtMeta : FHIRMeta) {
  srcNi -> tgtMeta.source = ('https://aphp.fr/fhir/Endpoint/dpi/SejourTable' + '/' + srcNi.toString()) "setSource";
}

group setSubjectReference(source srcPatientId , target newSubject) {
  srcPatientId -> newSubject.reference = ('Patient/' + %srcPatientId.toString()) "setReference";
}

group observationBiol(source srcBiology : BiologyTable, source srcPatient : FHIRPatient, source srcEncounter : FHIREncounter, target tgtObservation : FHIRObservation) {
  srcBiology -> tgtObservation.id = uuid() "setId";
  srcBiology.biologyNi as srcNi -> tgtObservation.meta as tgtMeta then setBiologyMeta(srcNi, tgtMeta) "setMeta";
  srcPatient.id as patientId -> tgtObservation.subject = create("Reference") as newSubject then setSubjectReference(patientId, newSubject) "setSubjectReference";
  srcEncounter.id as encounterId -> tgtObservation.encounter = create("Reference") as newEncounter then setEncounterReference(encounterId, newEncounter) "setEncounterReference";
  srcBiology.biologyDate as biologyDate -> tgtObservation.effective = biologyDate "setDate";
  srcBiology.biologyCode as biologyCode -> tgtObservation.code = cc('http://loinc.org', biologyCode) "setCode";
  srcBiology.biologyValue -> tgtObservation.value = create('Quantity') as tgtObsQuantity then {
    srcBiology.biologyValue as biologyValue -> tgtObsQuantity.value = biologyValue "setValue";
    srcBiology.biologyUnit as biologyUnit ->  tgtObsQuantity.unit = biologyUnit, tgtObsQuantity.system = 'http://unitsofmeasure.org' "setUnit";
  } "setQuantity";
}

group setBiologyMeta(source srcNi, target tgtMeta : FHIRMeta) {
  srcNi -> tgtMeta.source = ('https://aphp.fr/fhir/Endpoint/dpi/BiologyTable' + '/' + srcNi.toString()) "setSource";
}

group setEncounterReference(source srcEncounterId, target newEncounter) {
  srcEncounterId -> newEncounter.reference = ('Encounter/' + %srcEncounterId.toString()) "setReference";
}

group setEntryRequestAndFullUrl(source newRes, target tgtEntry) { 
  newRes.id as newResId -> (%newRes.type().name) as resourceType then {
    newResId -> tgtEntry.fullUrl = (%resourceType + '/' + %newResId) "setFullUrl";
    newResId -> tgtEntry.request as tgtEntryRequest, 
      tgtEntryRequest.method = 'POST',
      tgtEntryRequest.url = (%resourceType + '/' + %newResId) "setRequest" ;
  } "setResourceType" ;
}